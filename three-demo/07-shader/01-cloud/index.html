<!DOCTYPE html>
<html>
<head>
  <meta charset="UTF-8">
  <title>着色器-云层</title>
  <style>
    html, body{
      padding:  0;
      margin: 0;
      overflow: hidden;
    }
  </style>
</head>
<body>
<div id="demo"></div>

<script id="test3_fragment_shader" type="x-shader/x-fragment">
// Created by inigo quilez - iq/2013
// License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.

// Volumetric clouds. It performs level of detail (LOD) for faster rendering
uniform float iTime;
uniform vec2 iResolution;
uniform sampler2D iChannel0;
vec2 iMouse = vec2(0.0, 0.0);
float noise( in vec3 x )
{
    vec3 p = floor(x);
    vec3 f = fract(x);
	f = f*f*(3.0-2.0*f);

	vec2 uv = (p.xy+vec2(37.0,17.0)*p.z) + f.xy;
    vec2 rg = texture2D( iChannel0, (uv+ 0.5)/256.0, 0. ).yx;

	return -1.0+2.0*mix( rg.x, rg.y, f.z );
}

float map5( in vec3 p )
{
	vec3 q = p - vec3(0.0,0.1,1.0)*iTime;
	float f;
    f  = 0.50000*noise( q ); q = q*2.02;
    f += 0.25000*noise( q ); q = q*2.03;
    f += 0.12500*noise( q ); q = q*2.01;
    f += 0.06250*noise( q ); q = q*2.02;
    f += 0.03125*noise( q );
	return clamp( 1.5 - p.y - 2.0 + 1.75*f, 0.0, 1.0 );
}

float map4( in vec3 p )
{
	vec3 q = p - vec3(0.0,0.1,1.0)*iTime;
	float f;
    f  = 0.50000*noise( q ); q = q*2.02;
    f += 0.25000*noise( q ); q = q*2.03;
    f += 0.12500*noise( q ); q = q*2.01;
    f += 0.06250*noise( q );
	return clamp( 1.5 - p.y - 2.0 + 1.75*f, 0.0, 1.0 );
}
float map3( in vec3 p )
{
	vec3 q = p - vec3(0.0,0.1,1.0)*iTime;
	float f;
    f  = 0.50000*noise( q ); q = q*2.02;
    f += 0.25000*noise( q ); q = q*2.03;
    f += 0.12500*noise( q );
	return clamp( 1.5 - p.y - 2.0 + 1.75*f, 0.0, 1.0 );
}
float map2( in vec3 p )
{
	vec3 q = p - vec3(0.0,0.1,1.0)*iTime;
	float f;
    f  = 0.50000*noise( q ); q = q*2.02;
    f += 0.25000*noise( q );;
	return clamp( 1.5 - p.y - 2.0 + 1.75*f, 0.0, 1.0 );
}

vec3 sundir = normalize( vec3(-1.0,0.0,-1.0) );

vec4 integrate( in vec4 sum, in float dif, in float den, in vec3 bgcol, in float t )
{
    // lighting
    vec3 lin = vec3(0.65,0.7,0.75)*1.4 + vec3(1.0, 0.6, 0.3)*dif;
    vec4 col = vec4( mix( vec3(1.0,0.95,0.8), vec3(0.25,0.3,0.35), den ), den );
    col.xyz *= lin;
    col.xyz = mix( col.xyz, bgcol, 1.0-exp(-0.003*t*t) );
    // front to back blending
    col.a *= 0.4;
    col.rgb *= col.a;
    return sum + col*(1.0-sum.a);
}

#define MARCH(STEPS,MAPLOD) for(int i=0; i<STEPS; i++) { vec3  pos = ro + t*rd; if( pos.y<-3.0 || pos.y>2.0 || sum.a > 0.99 ) break; float den = MAPLOD( pos ); if( den>0.01 ) { float dif =  clamp((den - MAPLOD(pos+0.3*sundir))/0.6, 0.0, 1.0 ); sum = integrate( sum, dif, den, bgcol, t ); } t += max(0.05,0.02*t); }

vec4 raymarch( in vec3 ro, in vec3 rd, in vec3 bgcol, in ivec2 px )
{
	vec4 sum = vec4(0.0);

	float t = 0.0;

    MARCH(30,map5);
    MARCH(30,map4);
    MARCH(30,map3);
    MARCH(30,map2);

    return clamp( sum, 0.0, 1.0 );
}

mat3 setCamera( in vec3 ro, in vec3 ta, float cr )
{
	vec3 cw = normalize(ta-ro);
	vec3 cp = vec3(sin(cr), cos(cr),0.0);
	vec3 cu = normalize( cross(cw,cp) );
	vec3 cv = normalize( cross(cu,cw) );
    return mat3( cu, cv, cw );
}

vec4 render( in vec3 ro, in vec3 rd, in ivec2 px )
{
    // background sky
	float sun = clamp( dot(sundir,rd), 0.0, 1.0 );
	vec3 col = vec3(0.6,0.71,0.75) - rd.y*0.2*vec3(1.0,0.5,1.0) + 0.15*0.5;
	col += 0.2*vec3(1.0,.6,0.1)*pow( sun, 8.0 );

    // clouds
    vec4 res = raymarch( ro, rd, col, px );
    col = col*(1.0-res.w) + res.xyz;

    // sun glare
	col += 0.2*vec3(1.0,0.4,0.2)*pow( sun, 3.0 );

    return vec4( col, 1.0 );
}

void main()
{
    vec2 p = (-iResolution.xy + 2.0*gl_FragCoord.xy)/ iResolution.y;

    vec2 m = iMouse.xy/iResolution.xy;

    // camera
    vec3 ro = 4.0*normalize(vec3(sin(3.0*m.x), 0.4*m.y, cos(3.0*m.x)));
	vec3 ta = vec3(0.0, -1.0, 0.0);
    mat3 ca = setCamera( ro, ta, 0.0 );
    // ray
    vec3 rd = ca * normalize( vec3(p.xy,1.5));

    gl_FragColor = render( ro, rd, ivec2(gl_FragCoord-0.5) );
}
</script>
<script id="test3_vertexShader" type="x-shader/x-vertex">
	void main()
	{
		vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );
		gl_Position = projectionMatrix * mvPosition;
	}
</script>

<script src="../../common/three.js"></script>
<script type="text/javascript">
    var width = 0; // 画布的宽度
    var height = 0; // 画布的高度
    var scene = null; // 场景
    var camera = null; // 摄像机
    var renderer = null; // 渲染器
    var geom = null; // 几何体
    var uniforms = null; // 着色器使用参数
    var clock = new THREE.Clock(); // 时钟

    init();

    // 窗口大小改变后重新初始化
    window.addEventListener('resize', init);

    function init() {
        width = window.innerWidth; // 画布的宽度
        height = window.innerHeight; // 画布的高度

        /* 场景 */
        scene = createScene();

        /* 摄像机 */
        camera = createCamera();
        scene.add(camera); // 场景中添加相机

        /* 几何体 */
        geom = createGeom();
        scene.add(geom); // 场景中添加几何体

        /* 渲染器 */
        renderer = createRenderer();

        render();
    }

    /* 创建场景 */
    function createScene () {
        var scene = new THREE.Scene();
        // 坐标轴
        var axesHelper=new THREE.AxesHelper(10);
        scene.add(axesHelper);

        /*// 网格
        var gridHelper=new THREE.GridHelper(100, 20, 0xffff00, 0x808080);
        gridHelper.position.set(-2, 0, -2);
        scene.add(gridHelper);*/
        return scene;
    }
    /* 创建摄像机 */
    function createCamera () {
        var camera = new THREE.PerspectiveCamera(45, width/height, 1, 2000);
        camera.position.set(0,0,360);
        camera.lookAt(new THREE.Vector3(0, 0, 0));
        return camera
    }

    /* 创建渲染器 */
    function createRenderer () {
        var renderer = new THREE.WebGLRenderer({antialias:true});
        renderer.setSize(width, height);
        var parentElement = document.getElementById('demo');
        parentElement.innerHTML = '';
        parentElement.appendChild(renderer.domElement);
        return renderer;
    }

    /* 创建几何体 */
    function createGeom () {
        var texture = new THREE.TextureLoader().load( "./cloud_noise.png");
        texture.wrapS = THREE.RepeatWrapping;
        texture.wrapT = THREE.RepeatWrapping;

        uniforms = {
            iTime: { value: 1.0 },
            iResolution: { value: new THREE.Vector2(width * 1.0, height * 1.0)},
            iChannel0: {value: texture}
        }

        var material = new THREE.ShaderMaterial( {
            uniforms: uniforms,
            vertexShader: document.getElementById( 'test3_vertexShader' ).textContent,
            fragmentShader: document.getElementById('test3_fragment_shader').textContent
        });

        var geom = new THREE.PlaneBufferGeometry(width, height);
        var mesh = new THREE.Mesh( geom, material );
        return mesh;
    }

    /* 渲染 */
    function render() {
        // 获得距离上次调用的时间间隔
        var delta = clock.getDelta();
        uniforms.iTime.value += delta;
        renderer.render(scene, camera);
        requestAnimationFrame(render);
    }
</script>
</body>
